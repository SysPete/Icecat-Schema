#!/usr/bin/env perl

use warnings;
use strict;
use lib 'lib';

use FindBin;
use YAML qw/LoadFile/;
use Gzip::Faster;
use Icecat::Schema;
use WWW::Mechanize::Sleepy;

my $conf_data = LoadFile("$FindBin::Bin/../icecat.yaml");
my @required = qw/username password category_id/;

foreach my $k (@required) {
    die "Missing $k (required fields:" . join(", ", @required)
      unless defined $conf_data->{$k};
}

my $schema = Icecat::Schema->connect("ICECAT");
my $mech   = WWW::Mechanize::Sleepy->new(
    autocheck => 0,
    sleep => '1..5',
);
$mech->credentials( $conf_data->{username}, $conf_data->{password} );

my $topcat = $schema->resultset('Category')->find( $conf_data->{category_id} );

my $uri = "http://data.icecat.biz/export/freexml.int/EN/";

my $count = 0;

sub recurse {
    my $cat = shift;

    my $products = $cat->products;

    while ( my $product = $products->next ) {
        my $id   = $product->product_id;
        my $file = "xml/products/${id}.xml";
        next if -f $file;

        $mech->get( "${uri}${id}.xml" );

        if ( $mech->success ) {
            $mech->save_content( $file, binary => 1 );
            print "Count: $count\n" unless ++$count % 100;
        }
    }

    foreach my $child ( $cat->children ) {
        recurse($child);
    }
}

recurse($topcat);

